# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=graphviz
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
         #"${MINGW_PACKAGE_PREFIX}-${_realname}-docs")
pkgver=14.1.1
pkgrel=2
pkgdesc="Graph Visualization Software (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.graphviz.org/'
msys2_repository_url="https://gitlab.com/graphviz/graphviz"
msys2_references=(
  'archlinux: graphviz'
  'cpe: cpe:/a:graphviz:graphviz'
)
license=('spdx:EPL-1.0')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-devil"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-freeglut"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-gettext-runtime"
         "${MINGW_PACKAGE_PREFIX}-ghostscript"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gts"
         "${MINGW_PACKAGE_PREFIX}-libgd"
         "${MINGW_PACKAGE_PREFIX}-libltdl"
         "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-poppler"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             #"${MINGW_PACKAGE_PREFIX}-lua" problem with distinguishing between the two versions we have and links to the wrong version sigh
             "${MINGW_PACKAGE_PREFIX}-qt6-base"
             "${MINGW_PACKAGE_PREFIX}-swig"
             #"${MINGW_PACKAGE_PREFIX}-ruby" unable to find ruby's pkgconfig file looks for ruby.pc no ruby$(VERSION).pc as it is named now 
             #"${MINGW_PACKAGE_PREFIX}-tcl" needs a path to tclsh.sh
             "groff")
optdepends=("${MINGW_PACKAGE_PREFIX}-qt6-base: gvedit"
            "${MINGW_PACKAGE_PREFIX}-python: for python modules")
install=${_realname}-${MSYSTEM}.install
source=(https://gitlab.com/graphviz/graphviz/-/archive/${pkgver}/graphviz-${pkgver}.tar.bz2)
noextract=("graphviz-${pkgver}.tar.bz2")
sha256sums=('975f4b9a7a7a7b614900ecd12919ccbd36a680818cc6fc527389b93e919410f0')

_apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  echo "Extracting graphviz-${pkgver}.tar.bz2 ..."
  tar -xjf graphviz-${pkgver}.tar.bz2 || true

  cd "${srcdir}"/${_realname}-${pkgver}
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DENABLE_LTDL=ON \
      -DENABLE_PYTHON=ON \
      -DENABLE_SWIG=ON \
      -DWITH_EXPAT=ON \
      -DWITH_GHOSTSCRIPT=ON \
      -DWITH_GVEDIT=ON \
      -DWITH_POPPLER=ON \
      -DWITH_RSVG=ON \
      -DWITH_WEBP=ON \
      -DWITH_ZLIB=ON \
      -DENABLE_D=OFF \
      -DENABLE_GO=OFF \
      -DENABLE_GUILE=OFF \
      -DENABLE_JAVA=OFF \
      -DENABLE_JAVASCRIPT=OFF \
      -DENABLE_LUA=OFF \
      -DENABLE_PERL=OFF \
      -DENABLE_PHP=OFF \
      -DENABLE_R=OFF \
      -DENABLE_RUBY=OFF \
      -DENABLE_SHARP=OFF \
      -DENABLE_TCL=OFF \
      -DWITH_GDK=OFF \
      -DWITH_GTK=OFF \
      -DWITH_SMYRNA=OFF \
      -DWITH_X=OFF \
      -Duse_win_pre_inst_libs=OFF \
      -Dinstall_win_dependency_dlls=OFF \
      -S ${_realname}-${pkgver} \
      -B build-${MSYSTEM}

  cmake --build build-${MSYSTEM}
}

package_graphviz() {
  DESTDIR="${pkgdir}" cmake --install build-${MSYSTEM}

  local _pythonpath=$(python -c "import sysconfig; print(sysconfig.get_path('platlib'))")
  mkdir -p "${pkgdir}"$(cygpath ${_pythonpath})
  cp "${pkgdir}"${MINGW_PREFIX}/lib/graphviz/python3/* "${pkgdir}"$(cygpath ${_pythonpath})

  # split docs
  # mkdir -p dest${MINGW_PREFIX}/share
  # mv "${pkgdir}${MINGW_PREFIX}"/share/doc dest${MINGW_PREFIX}/share/doc
}

package_graphviz-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  optdepends=()

  cd "${srcdir}"/build-${MSYSTEM}
  mv dest/* "${pkgdir}"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
