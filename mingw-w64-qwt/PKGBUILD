# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ingo Maindorfer <ingo@liquidcooling.de>
# Based on work by AlexWMF <alexxwmf@gmail.com>

_realname=qwt
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-qt5"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-qt6")
_qwt_ver=6.3.0
pkgver=6.3.0+r0
pkgrel=1
pkgdesc="Qt Widgets for Technical Applications (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=("custom:Qwt")
url="https://qwt.sourceforge.io/"
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-qt5-base"
             "${MINGW_PACKAGE_PREFIX}-qt5-svg"
             "${MINGW_PACKAGE_PREFIX}-qt5-tools"
             "${MINGW_PACKAGE_PREFIX}-qt6-base"
             "${MINGW_PACKAGE_PREFIX}-qt6-svg"
             "${MINGW_PACKAGE_PREFIX}-qt6-tools"
             "git")
options=(!buildflags)
source=("${_realname}::git+https://github.com/MehdiChinoune/qwt#branch=cmake")
sha256sums=('SKIP')

build() {
  # qwt-qt5
  mkdir -p ${srcdir}/build-${MSYSTEM}-qt5 && cd ${srcdir}/build-${MSYSTEM}-qt5

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DQWT_QT_VERSION_MAJOR=5 \
    -DQWT_NAME_SUFFIX="-qt5" \
    ../${_realname}

  ${MINGW_PREFIX}/bin/cmake --build .

  # qwt-qt6
  mkdir -p ${srcdir}/build-${MSYSTEM}-qt6 && cd ${srcdir}/build-${MSYSTEM}-qt6

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DQWT_QT_VERSION_MAJOR=6 \
    -DQWT_NAME_SUFFIX="-qt6" \
    ../${_realname}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package_qwt-qt5() {
  pkgdesc="Qt Widgets for Technical Applications - Qt5 (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
           "${MINGW_PACKAGE_PREFIX}-qt5-base"
           "${MINGW_PACKAGE_PREFIX}-qt5-svg")
  optdepends=("${MINGW_PACKAGE_PREFIX}-qt5-tools: For Designer plugin")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake --install ${srcdir}/build-${MSYSTEM}-qt5

  install -Dm644 ${srcdir}/${_realname}/COPYING \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-qt5/COPYING
}

package_qwt-qt6() {
  pkgdesc="Qt Widgets for Technical Applications - Qt6 (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
           "${MINGW_PACKAGE_PREFIX}-qt6-base"
           "${MINGW_PACKAGE_PREFIX}-qt6-svg")
  optdepends=("${MINGW_PACKAGE_PREFIX}-qt6-tools: For Designer plugin")

  DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake --install ${srcdir}/build-${MSYSTEM}-qt6

  install -Dm644 ${srcdir}/${_realname}/COPYING \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-qt6/COPYING
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
