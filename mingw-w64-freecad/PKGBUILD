# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=freecad
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.1rc2
pkgrel=1
_AM_commit=8d35b2ec
_OS_commit=30e9b64e
pkgdesc="Free and Opensource multiplatform 3D parametric modeler (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://freecadweb.org/'
msys2_repository_url="https://github.com/FreeCAD/FreeCAD"
msys2_references=(
  'archlinux: freecad'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-boost-libs"
         "${MINGW_PACKAGE_PREFIX}-coin"
         "${MINGW_PACKAGE_PREFIX}-fmt"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-med"
         "${MINGW_PACKAGE_PREFIX}-omp"
         "${MINGW_PACKAGE_PREFIX}-opencascade"
         "${MINGW_PACKAGE_PREFIX}-openscad"
         "${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-numpy"
         "${MINGW_PACKAGE_PREFIX}-python-packaging"
         "${MINGW_PACKAGE_PREFIX}-python-pivy"
         "${MINGW_PACKAGE_PREFIX}-pyside6"
         "${MINGW_PACKAGE_PREFIX}-qt6-base"
         "${MINGW_PACKAGE_PREFIX}-qt6-svg"
         "${MINGW_PACKAGE_PREFIX}-xerces-c"
         "${MINGW_PACKAGE_PREFIX}-vtk"
         "${MINGW_PACKAGE_PREFIX}-yaml-cpp"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-curl"
             "${MINGW_PACKAGE_PREFIX}-eigen3"
             "${MINGW_PACKAGE_PREFIX}-fast_float"
             "${MINGW_PACKAGE_PREFIX}-hdf5"
             "${MINGW_PACKAGE_PREFIX}-icu"
             "${MINGW_PACKAGE_PREFIX}-nlohmann-json"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-pybind11"
             "${MINGW_PACKAGE_PREFIX}-python-matplotlib"
             "${MINGW_PACKAGE_PREFIX}-qt6-tools"
             "${MINGW_PACKAGE_PREFIX}-swig"
             "${MINGW_PACKAGE_PREFIX}-microsoft-gsl"
             "${MINGW_PACKAGE_PREFIX}-utf8cpp")
source=("https://github.com/FreeCAD/FreeCAD/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        "https://github.com/FreeCAD/AddonManager/archive/${_AM_commit}/AddonManager-${_AM_commit}.tar.gz"
        "https://github.com/FreeCAD/OndselSolver/archive/${_OS_commit}/OndselSolver-${_OS_commit}.tar.gz"
        "001-fix-find-openssl-and-curl.patch"
        "002-export-class.patch")
sha256sums=('SKIP'
            '4107c14ca95fdb35adc31aae779e5409fc9ef4752ad7a2727a002790d67c3ecc'
            '77646ca7d8cbc6dc4e8304439be2ff2b9aecf397e6349e63b3b06e65dfed79c3'
            'f8f6b05131ef83830a6628b2c085d34b1ca32641379a494bf2909ffc6223b9cf'
            'SKIP')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  mv AddonManager-*/* FreeCAD-${pkgver}/src/Mod/AddonManager/
  mv OndselSolver-*/* FreeCAD-${pkgver}/src/3rdParty/OndselSolver/

  cd FreeCAD-${pkgver}

  apply_patch_with_msg \
    001-fix-find-openssl-and-curl.patch \
    002-export-class.patch

  # opencascade defines HAVE_TBB which makes FreeCAD uses TBB without linking to it.
  sed -i "s|HAVE_TBB|HAVE_NOT_TBB|g" src/Mod/Import/App/ImportOCAF.cpp
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${CC} == clang ]]; then
    CXXFLAGS+=" -Wno-macro-redefined"
  else
    CXXFLAGS+=" -Wno-template-id-cdtor"
  fi

  CXXFLAGS+=" -D_USE_MATH_DEFINES -Wno-deprecated-declarations -Wno-unknown-pragmas" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;" \
    cmake -Wno-dev \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DFREECAD_QT_VERSION="6" \
      -DBUILD_ADDONMGR=OFF \
      -DBUILD_CLOUD=OFF \
      -DBUILD_DESIGNER_PLUGIN=OFF \
      -DBUILD_MATERIAL_EXTERNAL=OFF \
      -DBUILD_TEST=OFF \
      -DBUILD_WEB=OFF \
      -DENABLE_DEVELOPER_TESTS=OFF \
      -DINSTALL_TO_SITEPACKAGES=OFF \
      -DPython_EXECUTABLE=${MINGW_PREFIX}/bin/python \
      -DPython3_EXECUTABLE=${MINGW_PREFIX}/bin/python \
      "${_extra_config[@]}" \
      -S "FreeCAD-${pkgver}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"

  install -Dm644 FreeCAD-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE

  MSYS2_ARG_CONV_EXCL="-d" \
    python -m compileall -o 0 -o 1 -q -d"${MINGW_PREFIX}" "${pkgdir}${MINGW_PREFIX}"
}
